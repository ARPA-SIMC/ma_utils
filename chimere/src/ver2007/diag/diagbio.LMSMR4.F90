program diagbio

  use diagbio_common
  use diagbio_science
  use diagbio_megan    ! MEGAN Biogenic emissions module

  use chimere_consts
  use chimere_params  
  use netcdf
#ifdef IFORT
  use ifport
  use ifposix
#endif
  implicit none

#define NCERR(lnum) if(ncstat/=NF90_NOERR) call nc_err(ncstat,lnum,'diagbio.f90')

#define NFCHAR NF90_CHAR
#define NFLOAT NF90_FLOAT

  !  Input arguments

  integer            :: nhourrun ! Model run duration
  character(len=132) :: fniMETEO ! netCDF meteo file from exdom
  character(len=132) :: fniLDU   ! Landuse file
  character(len=132) :: fniMETINFOS         ! Input meteo information file
  character(len=132) :: fnoBIO
  character(len=132) :: fniMGNEF  ! MEGAN Input file

  namelist /args/ nhourrun,fniMETEO,fniLDU,fniMETINFOS,fnoBIO, &
                  fniMGNEF

  !  Local constants
  character(len=132),parameter :: fniARGS = 'diagbio.nml'
  real,parameter :: z0s = 5e-4       ! in saltation


  !  Constant netCDF attributes
  character(len=*),parameter :: title=      'CHIMERE SUITE'
  character(len=*),parameter :: subtitle=   'Bioemissions file'
  character(len=*),parameter :: generator=  'Generated by calbio'
  character(len=*),parameter :: conventions=''


  !****** types and variables
  !  Array dimensions, dynamic
  integer           :: nlevels    ! nb of levels in input netCDF file
  integer           :: dl         ! length of MM5 date string, according to input file


  ! unit numbers of ASCII and binary files
  integer :: ifn_args
  integer :: ifn_minf
  integer :: ifn_ldus

  ! Local arrays
  real,allocatable,dimension(:,:)   :: xlon  ! Longitudes of grid points
  real,allocatable,dimension(:,:)   :: xlat  ! Latitudes of grid points
!  real,allocatable,dimension(:,:,:) :: fland ! Land use fractions
  character(len=dlen) :: datebuf       ! to hold current date in MM5 format

  real :: czen          ! cosine of solar zenith angle
  real :: w10, wstar
  real :: vustas,cdnms
  real :: ustas
  real :: u10s,xsea
  integer :: itim       ! index of main time loop
  integer :: ime,izo    ! nmerid and nzonal indexes of grid points 
  integer :: ilan       ! index for land types
  integer :: ibio       ! index of biogenic parameters

  ! netcdf stuff
  ! d_ refers to diagmet output (calbio input)
  ! c_ refers to calbio output
  ! m_ refers to MEGAN input
  integer :: d_id,c_id,m_id                  ! IDs of input,output files
  integer :: ncstat                          ! return code for netCDF functions
  integer :: d_time_dimid,c_time_dimid       ! Time dimension IDs
  integer :: d_date_dimid,c_date_dimid       ! Date string dimension ID
  integer :: d_zonal_dimid,c_zonal_dimid     ! WE dimension IDs
  integer :: d_merid_dimid,c_merid_dimid     ! SN dimension IDs
  integer :: d_levels_dimid                  ! BT dimension IDs
  integer :: c_bio_dimid                     ! bio species dimension id
  ! variables IDs
  integer :: d_times_varid,c_times_varid
  integer :: d_lon_varid,d_lat_varid,c_lon_varid,c_lat_varid
  integer :: d_atte_varid
  integer :: d_w10s_varid
  integer :: d_usta_varid
  integer :: d_wsta_varid
  integer :: d_tem2_varid
  integer :: d_soim_varid
  integer :: c_biom_varid
  ! IDs for MEGAN
  integer :: d_swrd_varid    ! SW radiation
  integer,dimension(nbiomgn) :: m_ef_varid
  integer,dimension(12) :: m_lai_varid

  integer :: d_times                                   ! number of records in input file
  integer,allocatable,dimension(:) :: d_stvec          ! start vectors for R/W functions
  integer,allocatable,dimension(:) :: d_cntvec         ! count vectors for R/W functions
  character(len=1024) :: history,history_in
  integer :: hin_len

  ! misc
  integer :: idnow      ! current date,expected
  integer :: jyear,jmon,jday,jhour
  integer :: ierr       ! return code for non-netCDF I/O
  integer :: np
  ! MEGAN
  logical :: megan      ! MEGAN flag
  integer :: ndays,nhday,m_day,id1jan,doy

  ! System information stuff
  character(len=32) :: systime
  character(len=64) :: hname
  character(len=32) :: usrname
  character(len=255) :: cwd

  ! functions
  real,external :: cosineza
  integer,external :: interdat  ! For MEGAN

  !integer :: ifn_testemi


  !*****************************************************************************************
  ! Read input arguments
  call opfi(ifn_args,fniARGS,'f','o')
  read(ifn_args,nml=args,iostat=ierr)
  if(ierr/=0) then
     print *,'diagbio: problem reading arguments namelist'
     stop
  endif
  close(ifn_args)

  ! Number of simulation days
  ! For later use with MEGAN
  ndays=nhourrun/24+1
write(6,*)' sono 1'

  ! Get various system informations
  call get_system(usrname,hname,systime,cwd)

  ! Reading input information
  call opfi(ifn_minf,fniMETINFOS,'f','o')
  read(unit=ifn_minf,nml=metoptions)
  close(ifn_minf)

  ! Terrain files
  call opfi(ifn_ldus,fniLDU,'f','o')

  ! Open meteo netCDF input file from diagmet
  ncstat=nf90_open(fniMETEO,NF90_NOWRITE,d_id)
  NCERR(__LINE__)

  ! Read "history" attribute from input file
  call read_atts
write(6,*)' sono a12'

write(6,*)' sono 11112' ,fnoBIO
  ! Open meteo netCDF output file to chimere
  ncstat=nf90_create(fnoBIO,NF90_CLOBBER,c_id)
  NCERR(__LINE__)
write(6,*)' sono 11112' ,fnoBIO

  ! Verify that all required (mandatory and optional) variables
  ! are contained in the input netCDF file
  call verif_req
write(6,*)' sono 2'

  ! Reading dimensions from netCDF input file
  call read_dims
write(6,*)' sono 3'

  ! allocate memory for input and local fields
  call alloc_diagbio_locals

  ! Allocating memory for output fields
  call alloc_diagbio_globals

  ! Creating dimensions in output file
  call create_dims

  ! Defining variables in output files
  call define_out_vars

  ! Create attributes
  call create_atts

  !! End netCDF definition mode
  ncstat=nf90_enddef(c_id)


  ! Reading coordinates from input netCDF file
  ncstat=nf90_get_var(d_id,d_lon_varid,xlon)
  NCERR(__LINE__)
  ncstat=nf90_get_var(d_id,d_lat_varid,xlat)
  NCERR(__LINE__)

  ! Reading land use fractions
  do ime=1,nmerid
     do izo=1,nzonal
        read(ifn_ldus,*,iostat=ierr)(fland(izo,ime,ilan),ilan=1,nlduse)
        if (ierr/=0) stop '*** calbio : error reading Land Use file'
     end do
  end do
  ! MEGAN pre-processing

    ! Read MEGAN Emission Factors and LAI
    ! These are time-independent variables
    call read_megan_eflai

    ! Calculate MEGAN daily input (temperature and radiation)
    ! Loop over time
    nhday=0
    m_day=1
    m_dtem=0.0
    m_drad=0.0
    do itim=1,nhourrun+1

       ! Date
       call reldat(opt%idstart,itim-1,idnow)
       call ddate(idnow,jyear,jmon,jday,jhour)

       ! Reading mandatory data
       call read_data

       ! Date check
       call check_date

       ! Update hour counter
       nhday=nhday+1

       ! Sum up hourly values
       m_dtem(:,:,m_day)=m_dtem(:,:,m_day)+d_tem2
       m_drad(:,:,m_day)=m_drad(:,:,m_day)+d_swrd

       ! If a day is completed compute mean
       if (nhday==24.or.itim==nhourrun+1) then
         ! Finalize mean
         m_dtem(:,:,m_day)=m_dtem(:,:,m_day)/float(nhday)
         m_drad(:,:,m_day)=m_drad(:,:,m_day)/float(nhday)
         ! Force daily average in case last hour is midnight
         if (nhday==1) then
           m_dtem(:,:,m_day)=m_dtem(:,:,m_day-1)
           m_drad(:,:,m_day)=m_drad(:,:,m_day-1)
         endif
         ! Update counters
         m_day=m_day+1
         nhday=0
       endif
     
    enddo  ! itim

  ! Write coordinates
  ncstat=nf90_put_var(c_id,c_lon_varid,xlon)
  NCERR(__LINE__)
  ncstat=nf90_put_var(c_id,c_lat_varid,xlat)
  NCERR(__LINE__)


  ! Loop on time

  m_day=1  ! MEGAN day counter
  nhday=0  ! MEGAN hour counter
  do itim=1,nhourrun+1

     ! Date
     call reldat(opt%idstart,itim-1,idnow)
     call ddate(idnow,jyear,jmon,jday,jhour)
     ! Date of Jan 1st
     call rdate(jyear,1,1,0,id1jan)
     ! Julian day (Day Of Year)
     doy=interdat(id1jan,idnow)/24+1
     ! Update MEGAN hour counter
     nhday=nhday+1

     ! Reading mandatory data
     call read_data

     ! Date check
     call check_date
     
     ! Loop on grid points
     np=0
     do ime=1,nmerid
        do izo=1,nzonal
           np=np+1

           ! cosine of zenith angle
           czen = cosineza(xlat,xlon,idnow,jhour,izo,ime)

           ! Saltation u*, parametrized from V 10m
           w10   = d_w10s(izo,ime)
           wstar = d_wsta(izo,ime)
           cdnms = vkarm/log(10./z0s)
           vustas = max(w10,woff)
           ustas = cdnms*sqrt(vustas**2+(1.2*wstar)**2)

           ! Let us show we are alive ...
           if(np.eq.opt%nsho) then
              print 888,idnow,ustas
888           format(i10,f7.3)
           endif

           ! Parameters for sea salts
           u10s = max(w10,woff)
           xsea = fland(izo,ime,9) ! Be carefull to sea salts over in land water


           ! Biogenic emissions
           call calculate_bioemissions(ustas, czen, jmon, u10s, xsea, izo, ime, fland)

           ! MEGAN Biogenic emissions
           call megan_bioemis(izo,ime,jmon,m_day,czen,doy)

        end do !izo=1,nzonal
     end do !ime=1,nmerid


     ! Writing to output file
     call write_data

     ! Update MEGAN day counter
       if (nhday==24.or.itim==nhourrun+1) then
         m_day=m_day+1
         nhday=0
       endif

  end do !itim=1,nhourrun+1

  call dealloc_diagbio_locals
  call dealloc_diagbio_globals

  ncstat=nf90_close(d_id)
  NCERR(__LINE__)
  ncstat=nf90_close(c_id)
  NCERR(__LINE__)


  !*****************************************************************************************

contains

  !****************************************************
  subroutine read_atts
    ! Read "history" attribute from input file
    ncstat=nf90_inquire_attribute(d_id,NF90_GLOBAL, 'history', len=hin_len)
    NCERR(__LINE__)
    ncstat=nf90_get_att(d_id,NF90_GLOBAL, 'history', history_in)
    NCERR(__LINE__)
    history_in=history_in(1:hin_len)
  end subroutine read_atts

  !****************************************************
  subroutine verif_req
    write(6,*)'a'

#define NOTMAND(string) if (ncstat/=NF90_NOERR) call not_mand(string)

    ncstat=nf90_inq_varid(d_id,'Times', d_times_varid)
    NOTMAND('Times')
    write(6,*)'a'
    ncstat=nf90_inq_varid(d_id, 'lon',    d_lon_varid)
    NOTMAND('lon')
    write(6,*)'a'
    ncstat=nf90_inq_varid(d_id, 'lat',    d_lat_varid)
    NOTMAND('lat')
    write(6,*)'a'
    ncstat=nf90_inq_varid(d_id, 'tem2',  d_tem2_varid)
    NOTMAND('tem2')
    write(6,*)'a'
    ncstat=nf90_inq_varid(d_id, 'atte',  d_atte_varid)
    NOTMAND('atte')
    write(6,*)'a'
    ncstat=nf90_inq_varid(d_id, 'usta',  d_usta_varid)
    NOTMAND('usta')
    write(6,*)'a'
    ncstat=nf90_inq_varid(d_id, 'wsta',  d_wsta_varid)
    NOTMAND('wsta')
    write(6,*)'a'
    ncstat=nf90_inq_varid(d_id, 'w10s',  d_w10s_varid)
    NOTMAND('w10s')
    write(6,*)'a'
    ncstat=nf90_inq_varid(d_id, 'soim',  d_soim_varid)
    NOTMAND('soim')
    write(6,*)'a'
    ! SW radiation for MEGAN
    ncstat=nf90_inq_varid(d_id, 'swrd',  d_swrd_varid)
    NOTMAND('swrd')
    write(6,*)'a'

  end subroutine verif_req

  !****************************************************
  subroutine not_mand(vname)
    character*(*) :: vname
    print *
    print *, 'diagbio.F90 : meteo file does not contain'
    print *, 'the mandatory variable ',vname
    stop
  end subroutine not_mand
  
  !****************************************************
  subroutine read_dims

    integer :: we,sn

    ! IDs
    ncstat=nf90_inq_dimid(d_id, 'Time',        d_time_dimid)
    NCERR(__LINE__)
    ncstat=nf90_inq_dimid(d_id, 'DateStrLen',  d_date_dimid)
    NCERR(__LINE__)
    ncstat=nf90_inq_dimid(d_id, 'west_east',   d_zonal_dimid)
    NCERR(__LINE__)
    ncstat=nf90_inq_dimid(d_id, 'south_north', d_merid_dimid)
    NCERR(__LINE__)
    ncstat=nf90_inq_dimid(d_id, 'bottom_top',  d_levels_dimid)
    NCERR(__LINE__)
    ! lengths
    ! global variables nzonal and nmerid, local variable nlevels
    ! are obtained from input file header
    ncstat=nf90_inquire_dimension(d_id, d_time_dimid,  len=d_times)
    NCERR(__LINE__)
    ncstat=nf90_inquire_dimension(d_id, d_date_dimid,  len=dl)
    NCERR(__LINE__)
    if (dl/=dlen) stop 'calbio : date format error in input file'
    ncstat=nf90_inquire_dimension(d_id, d_zonal_dimid, len=we)
    NCERR(__LINE__)
    if (we/=nzonal) stop 'calbio : WE dimension error in input file'
    ncstat=nf90_inquire_dimension(d_id, d_merid_dimid, len=sn)
    NCERR(__LINE__)
    if (we/=nzonal) stop 'calbio : SN dimension error in input file'
    ncstat=nf90_inquire_dimension(d_id, d_levels_dimid,len=nlevels)
    NCERR(__LINE__)

  end subroutine read_dims

  !****************************************************
  subroutine alloc_diagbio_locals

    ! allocate memory for input fields
    allocate(d_tem2(nzonal,nmerid))
    allocate(d_atte(nzonal,nmerid))
    allocate(d_usta(nzonal,nmerid))
    allocate(d_w10s(nzonal,nmerid))
    allocate(d_soim(nzonal,nmerid))
    allocate(d_swrd(nzonal,nmerid))        ! SW radiation for MEGAN
    allocate(m_ef(nbiomgn,nzonal,nmerid))
    allocate(m_lai(12,nzonal,nmerid))
    allocate(m_dtem(nzonal,nmerid,ndays))
    allocate(m_drad(nzonal,nmerid,ndays))
    ! allocate memory for local fields
    allocate(xlon (       nzonal,nmerid))
    allocate(xlat (       nzonal,nmerid))
    allocate(fland(       nzonal,nmerid,nlduse))

  end subroutine alloc_diagbio_locals

  !****************************************************
  subroutine dealloc_diagbio_locals

    ! deallocate memory for input fields
    deallocate(d_tem2)
    deallocate(d_atte)
    deallocate(d_usta)
    deallocate(d_w10s)
    deallocate(d_soim)
    deallocate(d_swrd)   ! SW radiation for MEGAN
    deallocate(m_ef)
    deallocate(m_lai)
    deallocate(m_dtem)
    deallocate(m_drad)
    ! deallocate memory for local fields
    deallocate(xlon )
    deallocate(xlat )
    deallocate(fland)

  end subroutine dealloc_diagbio_locals

  !****************************************************
  subroutine create_dims

    ncstat=nf90_def_dim(c_id,'Time',        NF90_UNLIMITED,c_time_dimid)
    NCERR(__LINE__)
    ncstat=nf90_def_dim(c_id,'DateStrLen',  dlen,          c_date_dimid)
    NCERR(__LINE__)
    ncstat=nf90_def_dim(c_id,'west_east',   nzonal,        c_zonal_dimid)
    NCERR(__LINE__)
    ncstat=nf90_def_dim(c_id,'south_north', nmerid,        c_merid_dimid)
    NCERR(__LINE__)
    ncstat=nf90_def_dim(c_id,'biospecies',  nemisb,        c_bio_dimid)
    NCERR(__LINE__)

  end subroutine create_dims

  !****************************************************
  subroutine define_out_vars

    ! Times
    ncstat=nf90_def_var(      &
         c_id,                &
         'Times',             &
         NFCHAR,              &
         (/c_date_dimid,c_time_dimid/),   &
         c_times_varid)
    NCERR(__LINE__)

    ! Longitude, latitude
    ncstat=nf90_def_var(      &
         c_id,                &
         'lon',               &
         NF90_FLOAT,          &
         (/c_zonal_dimid,c_merid_dimid/), &
         c_lon_varid)
    NCERR(__LINE__)
    ncstat=nf90_def_var(      &
         c_id,                &
         'lat',               &
         NF90_FLOAT,          &
         (/c_zonal_dimid,c_merid_dimid/), &
         c_lat_varid)
    NCERR(__LINE__)
    ! Biom
    ncstat=nf90_def_var(      &
         c_id,                &
         'emisb',             &
         NFLOAT,              &
         (/c_bio_dimid,c_zonal_dimid,c_merid_dimid,c_time_dimid/), &
         c_biom_varid)
    NCERR(__LINE__)

  end subroutine define_out_vars

  !****************************************************
  subroutine create_atts

    ncstat=nf90_put_att(c_id,NF90_GLOBAL,'Title',title)
    NCERR(__LINE__)
    ncstat=nf90_put_att(c_id,NF90_GLOBAL,'Sub-title',subtitle)
    NCERR(__LINE__)
    ncstat=nf90_put_att(c_id,NF90_GLOBAL,'Generating_process',generator)
    NCERR(__LINE__)
    ncstat=nf90_put_att(c_id,NF90_GLOBAL,'Conventions',conventions)
    NCERR(__LINE__)
    ncstat=nf90_put_att(c_id,NF90_GLOBAL,'Domain',domain)
    NCERR(__LINE__)

#if defined(IFORT)

    history = &
         'File '                                                        // &
         cwd(1:len_trim(cwd)) // '/'//fnoBIO(1:len_trim(fnoBIO))        // &
         ' was generated on '                                           // &
         systime(1:len_trim(systime))                                   // &
         ' by '                                                         // &
         usrname(1:len_trim(usrname))                                   // &
         ' on '                                                         // &
         hname(1:len_trim(hname))                                       // &
         ' from input file '                                            // &
         cwd(1:len_trim(cwd))//'/'//fniMETEO(1:len_trim(fniMETEO))      //'\n'C
    history = history(1:len_trim(history)-1) // history_in(1:len_trim(history_in)-1)

#elif (defined(G95)+defined(PGI))

    history = &
         'File '                                                        // &
         cwd(1:len_trim(cwd)) // '/'//fnoBIO(1:len_trim(fnoBIO))        // &
         ' was generated on '                                           // &
         systime(1:len_trim(systime))                                   // &
         ' by '                                                         // &
         usrname(1:len_trim(usrname))                                   // &
         ' on '                                                         // &
         hname(1:len_trim(hname))                                       // &
         ' from input file '                                            // &
         cwd(1:len_trim(cwd))//'/'//fniMETEO(1:len_trim(fniMETEO))
    history = history(1:len_trim(history)-1) // history_in(1:len_trim(history_in)-1)

#else

    history='unknown'

#endif

    ncstat=nf90_put_att(c_id,NF90_GLOBAL,'history',history(1:len_trim(history)))
    NCERR(__LINE__)

  end subroutine create_atts

  !****************************************************
  subroutine read_data
    implicit none

    ! Times
    ncstat=nf90_get_var(d_id,d_times_varid,datebuf,(/1,itim/),(/dlen,1/))
    NCERR(__LINE__)
    
    !! 2D variables
    allocate(d_stvec(3))
    allocate(d_cntvec(3))
    d_stvec=(/1,1,itim/)
    d_cntvec=(/nzonal,nmerid,1/)

    ncstat=nf90_get_var(d_id, d_tem2_varid, d_tem2, d_stvec,d_cntvec)
    NCERR(__LINE__)
    ncstat=nf90_get_var(d_id, d_atte_varid, d_atte, d_stvec,d_cntvec)
    NCERR(__LINE__)
    ncstat=nf90_get_var(d_id, d_usta_varid, d_usta, d_stvec,d_cntvec)
    NCERR(__LINE__)
    ncstat=nf90_get_var(d_id, d_wsta_varid, d_wsta, d_stvec,d_cntvec)
    NCERR(__LINE__)
    ncstat=nf90_get_var(d_id, d_w10s_varid, d_w10s, d_stvec,d_cntvec)
    NCERR(__LINE__)
    ncstat=nf90_get_var(d_id, d_soim_varid, d_soim, d_stvec,d_cntvec)
    NCERR(__LINE__)
    ncstat=nf90_get_var(d_id, d_swrd_varid, d_swrd, d_stvec,d_cntvec)
    NCERR(__LINE__)

    deallocate(d_stvec)
    deallocate(d_cntvec)

  end subroutine read_data

  !****************************************************
  subroutine check_date
    implicit none

    integer :: idc              ! current date,from netCDF input file
    integer :: mm5date2numeric  ! function to transform MM5 time into CHIMERE time

    idc=mm5date2numeric(datebuf)
    if (idc/=idnow) then
       print *,'*** calbio : Date Problem with Meteo File: '
       print *,'*** Expected date: ',idnow,' File date: ',idc
       stop
    endif

  end subroutine check_date

  !****************************************************
  subroutine write_data

    ncstat=nf90_put_var(c_id,c_times_varid,datebuf,(/1,itim/),(/dlen,1/))
    NCERR(__LINE__)
    ncstat=nf90_put_var(c_id, c_biom_varid, c_biom, &
         start=(/1,     1,     1,     itim/),       &
         count=(/nemisb,nzonal,nmerid,1   /) )
    NCERR(__LINE__)


    !write(ifn_testemi)idnow,(((c_biom(ibio,izo,ime),izo=1,nzonal),ime=1,nmerid),ibio=1,nemisb)


  end subroutine write_data

  !****************************************************
  subroutine read_megan_eflai

    ! Local variables
    integer :: ief,imon
    character(len=5) :: laivar

    ! Open netCDF file
    ncstat=nf90_open(fniMGNEF,NF90_NOWRITE,m_id) ; NCERR(__LINE__)

    ! Read variables IDs
    ncstat=nf90_inq_varid(m_id,'ISOP',m_ef_varid(1)) ; NCERR(__LINE__)
    ncstat=nf90_inq_varid(m_id,'APIN',m_ef_varid(2)) ; NCERR(__LINE__)
    ncstat=nf90_inq_varid(m_id,'BPIN',m_ef_varid(3)) ; NCERR(__LINE__)
    ncstat=nf90_inq_varid(m_id,'LIMO',m_ef_varid(4)) ; NCERR(__LINE__)
    ncstat=nf90_inq_varid(m_id,'OCIM',m_ef_varid(5)) ; NCERR(__LINE__)
    ncstat=nf90_inq_varid(m_id,'TCAR',m_ef_varid(6)) ; NCERR(__LINE__)
    ncstat=nf90_inq_varid(m_id,'SABI',m_ef_varid(7)) ; NCERR(__LINE__)
    ncstat=nf90_inq_varid(m_id,'MYRC',m_ef_varid(8)) ; NCERR(__LINE__)
    ncstat=nf90_inq_varid(m_id,  'NO',m_ef_varid(9)) ; NCERR(__LINE__)
    do imon=1,12
      if (imon<10) then
        write(laivar,'("LAI",I1)') imon
      else
        write(laivar,'("LAI",I2)') imon
      end if
      ncstat=nf90_inq_varid(m_id,laivar,m_lai_varid(imon)) ; NCERR(__LINE__)
    enddo

    ! Read variables
    do ief=1,nbiomgn
      ncstat=nf90_get_var(m_id,m_ef_varid(ief),m_ef(ief,:,:),&
             (/1,1/),(/nzonal,nmerid/))
      NCERR(__LINE__)
    enddo
    do imon=1,12
      ncstat=nf90_get_var(m_id,m_lai_varid(imon),m_lai(imon,:,:),&
             (/1,1,imon/),(/nzonal,nmerid,1/))
      NCERR(__LINE__)
    enddo

    ! Close netCDF file
    ncstat=nf90_close(m_id) ; NCERR(__LINE__)

  end subroutine read_megan_eflai

  !****************************************************

end program diagbio


